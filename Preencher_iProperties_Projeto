'===============================
' INIT_iProps_Resumo  –  FINAL
'===============================
' Roda só se o campo Título estiver vazio
Sub Main()
    ' Verifica se o título já está preenchido para evitar sobrescrita
    If Not String.IsNullOrWhiteSpace(iProperties.Value("Summary", "Title")) Then
        Exit Sub
    End If
    
    Dim titulo As String = PromptObrigatorio("Qual o título do projeto? EG.: PASSA PÃO", "Definir Título")
    If String.IsNullOrWhiteSpace(titulo) Then Exit Sub
    
    Dim cliente As String = PromptObrigatorio("Qual o cliente? EG.: GRUPO MATEUS", "Definir Empresa")
    If String.IsNullOrWhiteSpace(cliente) Then Exit Sub
    
    Dim categoria As String = PromptOpcional("Defina uma categoria. EG.: BASE METÁLICA", "Definir Categoria")
    Dim keywords As String = PromptOpcional("Algum detalhe adicional?", "Definir Keywords")
    
    PreencherResumo(titulo, cliente, categoria, keywords)
End Sub

Private Function PromptObrigatorio(prompt As String, title As String) As String
    Dim input As String = InputBox(prompt, title)
    If String.IsNullOrWhiteSpace(input) Then
        MessageBox.Show("Campo obrigatório não preenchido!", "Erro", MessageBoxButtons.OK, MessageBoxIcon.Warning)
        Return ""
    End If
    Return input.Trim()
End Function

Private Function PromptOpcional(prompt As String, title As String) As String
    Dim input As String = InputBox(prompt, title)
    Return If(String.IsNullOrWhiteSpace(input), "", input.Trim())
End Function

Private Sub PreencherResumo(titulo As String, cliente As String, categoria As String, keywords As String)
    iProperties.Value("Summary", "Title") = titulo
    iProperties.Value("Summary", "Author") = System.Environment.UserName
    iProperties.Value("Summary", "Manager") = "GILBERTO"
    iProperties.Value("Summary", "Company") = cliente
    iProperties.Value("Summary", "Category") = If(String.IsNullOrWhiteSpace(categoria), "GERAL", categoria)
    iProperties.Value("Summary", "Keywords") = If(String.IsNullOrWhiteSpace(keywords), "", keywords)
End Sub